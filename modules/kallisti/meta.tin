#nop --
#nop Class Definitions
#nop --

#var kallisti/meta-description {Meta assistance and advice}
#var kallisti/autobuf-help {
This module provides meta advice for every class.  It will suggest\n
the optimal choice of h/m/s for the lowest cost for most hero points.\n
\n
It also adds a metaTotal command to add up your metas, and metaReport\n
to endlessly disappoint you with how much xp and gold you've used.
}

#nop --
#nop Modloader Stuff
#nop --

#alias kallisti/meta-register {
	#if {@isloaded{queues}} {
		register_module kallisti/meta
	} {
		fail_module kallisti/meta Need queues module
	}
}
init BASE_HEROS preferences 0
init TARGET_HEROS preferences 0
init metaXPCost preferences 0
init metaAUCost preferences 0
init metaHero preferences 0

#list metaGains[Immortal] create 1.0 1.0 1.0
#list metaGains[Mage] create 3.5 5.5 3.5
#list metaGains[Wizard] create 3.5 5.5 3.5
#list metaGains[Necromancer] create 3.5 5.5 3.5

#list metaGains[Priest] create 4 5 3.5
#list metaGains[Templar] create 5 5 3
#list metaGains[Druid] create 4 5 4
#list metaGains[Prophet] create 3.5 6 3.5

#list metaGains[Rogue] create 5.5 1.0 6.0
#list metaGains[Assassin] create 5.5 1 6.5
#list metaGains[Demoniac] create 4.5 3.5 5.0
#list metaGains[Bard] create 4.5 3.5 5

#list metaGains[Fighter] create 6.0 1.0 5.5
#list metaGains[Barbarian] create 6.5 1.0 5.5
#list metaGains[Ranger] create 5.5 2.0 5.5
#list metaGains[Dreadlord] create 6.0 3.0 4.5
#list metaGains[Paladin] create 6.0 3.0 4.5
#list metaGains[Valkyrie] create 6.0 3.0 4.5

#list metaGains[Monastic] create 5.0 2.5 5.0
#list metaGains[Monk] create 5.0 3.0 5.0
#list metaGains[Samurai] create 6.0 2.5 4.5

#list metaHerosPer[Immortal] create 1.0 1.0 1.0

#list metaHerosPer[Fighter] create 2.45 0.10 1.65
#list metaHerosPer[Barbarian] create 2.45 0.1 1.65
#list metaHerosPer[Paladin] create 2.3 1.5 1.3
#list metaHerosPer[Dreadlord] create 2.3 1.3 1.5
#list metaHerosPer[Ranger] create 2.3 1.3 1.5
#list metaHerosPer[Valkyrie] create 2.3 1.5 1.3

#list metaHerosPer[Rogue] create 2.40 0.10 1.70
#list metaHerosPer[Assassin] create  2.4 0.1 1.7
#list metaHerosPer[Demoniac] create 2.25 1.6 1.55
#list metaHerosPer[Bard] create 2.1 1.7 1.5

#list metaHerosPer[Priest] create 2.0 2.45 0.85
#list metaHerosPer[Templar] create 2.0 2.2 1.0
#list metaHerosPer[Druid] create 1.9 2.35 0.95
#list metaHerosPer[Prophet] create 1.70 2.80 0.85

#list metaHerosPer[Mage] create 1.75 2.8 0.8
#list metaHerosPer[Necromancer] create 1.75 2.8 0.8
#list metaHerosPer[Wizard] create 1.75 2.8 0.8

#list metaHerosPer[Monastic] create 2.4 1.4 1.5
#list metaHerosPer[Monk] create 2.25 1.5 1.5
#list metaHerosPer[Samurai] create 2.4 0.2 1.95



#action {meta'd %1 to %2, cost = %3xp, %4gp} {
	#math {metaExpTotal} {$metaExpTotal+%3};
	#math {metaTotalGold} {$metaTotalGold+%4}
}

#tab metaTotal
#alias {metaTotal} {
	#var {metaExpTotal} {0};
	#var {metaTotalGold} {0};
	meta
}

#tab metaReport
#alias {metaReport} {

    #format {_line1} {<g23> Total exp Meta'd: <efc>%g} {$metaExpTotal};
    #format {_line2} {<g23>Total gold Meta'd: <fea>%-15g} {$metaTotalGold};
    #echo $_line1;
    #echo $_line2;
}

#tab metaCost 
#alias metaCost {
    
    #math {metaavg} { ($metaXPCost[hp] + $metaXPCost[mana] + $metaXPCost[stam]) / 3};
    #math tmp {$MSDP_EXPERIENCE / $metaavg};

    #echo {Current Meta Costs (est. @estimated_metas{} metas with current XP):};
    #echo { HitP: $metaXPCost[hp] ($metaHero[hp] per hero)};
    #echo { Mana: $metaXPCost[mana] ($metaHero[mana] per hero)};
    #echo { Stam: $metaXPCost[stam] ($metaHero[stam] per hero)};
    
}

#nop -- ---------------------------------------------------------------
#nop -- Autometa functions
#nop -- ---------------------------------------------------------------
#function one_meta_cost {
    #math {result} { ($metaXPCost[HitPoints] + $metaXPCost[Mana] + $metaXPCost[Stamina]) / 3};
}
#function estimated_metas {
    #math result {$MSDP_EXPERIENCE / @one_meta_cost{}}
}
#function metaHero {
    #switch {"%1"} {
        #case {"HitPoints"} {
            #math _metaHH {$metaGains[$MSDP_CLASS][1] * $metaHerosPer[$MSDP_CLASS][1]};
            #return $_metaHH
        };
        #case {"Mana"} {
            #math _metaHM {$metaGains[$MSDP_CLASS][2] * $metaHerosPer[$MSDP_CLASS][2]};
            #return $_metaHM
        };
        #case {"Stamina"} {
            #math _metaHS {$metaGains[$MSDP_CLASS][3] * $metaHerosPer[$MSDP_CLASS][3]};
            #return $_metaHS
        }
    }
}


#alias autometa {
    #var autometa on;
    list
}

#action {^Welcome to the Meta shop where you can enhance yourself in miraculous ways!$} {
    #var meta_screen 1;
    #var meta_borders 0
}

#action {^================================================================================$} {
    #if {$meta_screen == 1} {
        #if {$meta_borders == 1} {
            #var meta_screen 0;
            recMeta
        };
        #else {
            #var meta_borders 1
        }
    }
}

#action {{^Max(\w+) \[*.+\]\s+(\d+)\s+(\d+)\s+\d+\s+\d+\s+[0-9\.]+ avg$}} {
    #nop h/m/s = %2, exp = %3, au = %4;
    #var metaXPCost[%2] %3;
    #var metaAUCost[%2] %4;
    #math {metaHero[%2]} { $metaXPCost[%2] / @metaHero{%2} };
}

#alias {recMeta} {
    #if { ($metaHero[HitPoints] < $metaHero[Stamina]) & ($metaHero[HitPoints] < $metaHero[Mana])} {
        #var meta_rec HitPoints
    };
    #elseif { ($metaHero[Mana] < $metaHero[HitPoints]) & ($metaHero[Mana] < $metaHero[Stamina])} {
        #var meta_rec Mana
    };
    #elseif { ($metaHero[Stamina] < $metaHero[HitPoints]) & ($metaHero[Stamina] < $metaHero[Mana])} {
        #var meta_rec Stamina
    };
    #else {
        #var meta_rec None
    };
    #if {"$meta_rec"=="None"} {
        #show {<028>Unable to provide meta advice, check values in meta.tin.}
    };
    #else {
        #showme $metaHero[HitPoints] | $metaHero[Mana] | $metaHero[Stamina];
        #format {metarec_line} {%%80s} {<028>Meta <128>$meta_rec <028>for the most herps per experience point. <068>(@hNum{$metaHero[$meta_rec]} / per)};
        #showme {$metarec_line};
        _conditional_meta $meta_rec
    };
    #if {"$auto[meta]" == "on"} {
        meta $meta_rec
    }
}

#alias _conditional_meta {
    #if {&{MSDP_HERO_POINTS} && "$autometa" == "on" && ${MSDP_HERO_POINTS} < $TARGET_HEROS} {
        buy %1;
        list
    } {
        #if {${MSDP_HERO_POINTS} > $TARGET_HEROS} {
            #showme <cca>AUTOMETA DISABLED - reached $MSDP_HERO_POINTS hero points ($TARGET_HEROS is target).;
        } {
            #showme <cca>AUTOMETA DISABLED.;
        };
        #var autometa off
    }
}


#ACTION {You are nearing the cap for bankable xp, consider 'meta' or 'gain' now.} {
    #if { (20<=$MSDP_LEVEL) && ($MSDP_LEVEL<51) && ($profession[refining][auto]) } {
        goguild;gain;gain;gain;gain;remorttan
    };#else {
        nco return_and_meta
    }
}


#tab return_and_meta;
#alias return_and_meta {
    #var return_zone $MSDP_AREA_NAME;
    #if {$profession[refining][auto]} {
        #var return_zone Profession;
    };
    #switch {"$MSDP_AREA_NAME"} {
        #case {"The Wilderness"} {
            gt &08nohunt&--;
            #action {You find yourself back in Midgaard City.} {
                #unaction {You find yourself back in Midgaard City.};
                doMetaOrGain
            };
            #action {You find yourself back in Gummton Outpost.} {
                #unaction {You find yourself back in Gummton Outpost.};
                n2w;
                book cargo;
                #delay 5 recall;
            };
            recall
        };
        #case {"Gummton Outpost"} {
            recall;
            n2w;
            book cargo;
            #delay 5 recall;
        };
        #default {
            recall;
            doMetaOrGain;
        }
    }
}

#tab doMetaOrGain;
#alias doMetaOrGain {
    #show <131>Check Meta or Gain;
    #if { $MSDP_EXPERIENCE_TNL <= 0 } {
        #if {$MSDP_LEVEL < @getMaxRemortLevel{}} { 
            goGain; #nop REMORTS DONT NEED TO REMETA LEVELS
        } { 
            checkNobPoints 
        }
    };#else {
        returntostart;
    }
}

#alias goGain {
    #if {"$MSDP_ACCOUNT_NAME"=="Kruoill"} {
        visit house;
    } {
        home;
    };
    #if {$MSDP_LEVEL==50} {with 200000} {with $goldlevel[$MSDP_LEVEL]};
    egress;
    goguild;
    gain;
    $guildreturnpath;
    #delay 5 doMetaOrGain
}

#alias doMeta {
    2s14w;
    enter tree;
    open door;
    w;
    #if { ($MSDP_BANK_GOLD<100000000) } {
        with all
    } {
        with 10000000
    };
    rem all;
    #if {$MSDP_LEVEL<100} {
        levelreq 100;
        #delay 10 autometa
    };
    #switch {"$MSDP_CHARACTER_NAME"} {
        #case {"Kroll"} {
            #action {Your mana reserve expands with new power.} {buy mana};
            #action {Your heart pounds as your body grows more resilient.} {buy hit};
            #delay 10 {#show <131>BUY MANA;buy mana}
        };
        #case {"Crom"} {
            #action {Your mana reserve expands with new power.} {buy mana};
            #action {Your heart pounds as your body grows more resilient.} {buy hit};
            #delay 10 {#show <131>BUY MANA;buy hit}
        };
        #case {"Kaicha"} {
            list;
            #delay 15 {#show <131>AUTOMETA;#var TARGET_HEROS 40000;autometa}
        };
        #case {"Loke"} {
            list;
            #action {Your heart pounds as your body grows more resilient.} {buy hit};
            #delay 10 {#show <131>BUY HIT;buy hit}
        };
    }
}

#alias {returntostart} {
    #if {"$MSDP_ACCOUNT_NAME"=="Kruoill"} {
        visit house;
    } {
        home;
    }
    dep all;
    egress;
    #switch {"$return_zone"} {
        #case {"The Wilderness"} {
            #if {"$MSDP_ACCOUNT_NAME"=="Kruoill"} { 
                visit house;
            }
            {
                home;
            }
            with 100000;
            egress;
            3s2es;
            book gumm;;
            #action {Land is in sight!} {
                #delay 10 {#unaction {Land is in sight!};gt &08yeshunt&--}
            };
            #action {You wash up on shore...} {
                #if {"$MSDP_AREA_NAME"=="The Wilderness"} {gt &08yeshunt&--;#if {"$MSDP_ACCOUNT_NAME"=="Kruoill"} {true;holygate crom}};
                #else {recall;stand;returntostart;}
            }
        };
        #case {"Tellurian"} {8sw2se29s9w2sswswswdses4e;enter portal};
        #case {"Profession"} {recall;remorttan};
    };
}

#action {^The meta-sage tells you 'You have to remove ALL your items first.'} {dress 0;autometa}
#action {^The meta-sage tells you 'You don't have enough gold.} {list;#var autometa off;dep all;dress 1;16e2n;doMetaOrGain}
#action {^The meta-sage tells you 'You don't have enough experience.} {
    list;
    #var autometa off;
    dep all;
    wear all;
    #unaction {Your mana reserve expands with new power.};
    #unaction {Your heart pounds as your body grows more resilient.};
    #unaction {Your heart races with fresh energy reserves.};
    16e2n;
    returntostart
}
#action {^The meta-sage tells you 'Go level instead!'} {list;#var autometa off;dep all;dress 1}

#var forcemeta 0;

#alias checkNobPoints {
    #if { $MSDP_HERO_POINTS_TNL < 0} 
    { #if {$forcemeta} {doMeta} {goGain} } 
    { doMeta }
}

#alias setguildpath {
    #switch {"$MSDP_CLASS"} {
        #case {"Fighter"} {#var guildpath 2s2e2s;#var guildreturnpath 2n2w2n;#var basepath 2s2e2s};
        #case {"Rogue"} {#var guildpath 3s3w2s;#var guildreturnpath 2n3e3n;#var basepath 3s3w2s};
        #case {"Priest"} {#var guildpath swn;#var guildreturnpath sen;#var basepath swn};
        #case {"Mage"} {#var guildpath 2s2w2s;#var guildreturnpath 2n2e2n;#var basepath 2s2w2s};
        #case {"Monastic"} {#var guildpath 2s6e2s;#var guildreturnpath 2n6w2n;#var basepath 2s6e2s};
        #case {"Barbarian"} {#var guildpath 2s2e3s;#var guildreturnpath 3n2w2n;#var basepath 2s2e2s};
        #case {"Paladin"} {#var guildpath 2s2e2su;#var guildreturnpath d2n2w2n;#var basepath 2s2e2s};
        #case {"Dreadlord"} {#var guildpath 2s2e2sd;#var guildreturnpath u2n2w2n;#var basepath 2s2e2s};
        #case {"Ranger"} {#var guildpath 2s2e2se;#var guildreturnpath w2n2w2n;#var basepath 2s2e2s};
        #case {"Valkyrie"} {#var guildpath 2s2e2sw;#var guildreturnpath e2n2w2n;#var basepath 2s2e2s};
        #case {"Assassin"} {#var guildpath 3s3w3s;#var guildreturnpath 3n3e3n;#var basepath 3s3w2s};
        #case {"Demoniac"} {#var guildpath 3s3w2sw;#var guildreturnpath e2n3e3n;#var basepath 3s3w2s};
        #case {"Bard"} {#var guildpath 3s3w2se;#var guildreturnpath w2n3e3n;#var basepath 3s3w2s};
        #case {"Templar"} {#var guildpath swne;#var guildreturnpath wsen;#var basepath swn};
        #case {"Druid"} {#var guildpath swn2w;#var guildreturnpath 2esen;#var basepath swn};
        #case {"Prophet"} {#var guildpath sw2n;#var guildreturnpath 2sen;#var basepath swn};
        #case {"Necromancer"} {#var guildpath 2s2w2sd;#var guildreturnpath u2n2e2n;#var basepath 2s2w2s};
        #case {"Wizard"} {#var guildpath 2s2w2su;#var guildreturnpath s2n2e2n;#var basepath 2s2w2s};
        #case {"Monk"} {#var guildpath 2s6e2su;#var guildreturnpath d2n6w2n;#var basepath 2s6e2s};
        #case {"Samurai"} {#var guildpath 2s6e2se;#var guildreturnpath w2n6w2n;#var basepath 2s6e2s};
    }
}

#alias goguild {
    setguildpath;
    $guildpath;
}

#nop -- vim: syntax=tt
