#nop --
#nop Class Definitions
#nop --

#var remort-description {LoK remort to 60}
#var remort-help {coming soon}

#nop --
#nop Modloader Stuff
#nop --

#alias remort-register {
	#if {@isloaded{events}} {
		register_module remort
	} {
		#showme ERROR: events not loaded;
		fail_module remort
	}
}

#alias remort-unregister {
	event_remove_handler remort;
}

event_register {level gain} {remort} {handle_remort_level};

#var booster {};
#var refresher {Volva}
#var tanningmaxed 1;
#var spec_class barbarian;
#var time_bloodline_start $MSDP_UPTIME;

#tab start_bloodline;
#alias start_bloodline {
    #var time_bloodline_start $MSDP_UPTIME;
    #if {"%1"==""} {
        #show <131>You need to provide booster: Liminal/Kaicha
    };
    #else {
        #var booster %1;
        setguildpath; #nop returns #case {"Ranger"} {#var guildpath 2s2e2se;#var guildreturnpath w2n2w2n};
        #var tanningmaxed 1;
        prompt exp;
        toggl autoass;
        toggl GroupHits;
        toggl GroupMisses;
        toggl HitsToGroup;
        toggl MissesToGroup;
        toggl TargetMisses;
        toggl BriefCombat;
        toggl melee;
        toggl brief;
        setsummon group;
        sac long;
        sac breastplate;
        visit eremitage;u;get all chest;wear all;get all chest;wear all;put all chest;
        remort_quest_1
    }
}

#alias initialize_remort {
    #if {$MSDP_LEVEL<20} {
        #var bot[damage][single] kill;#var autotooth 0;bot_init;botrole leader;botpoke 0;group all;setguildpath;layout right
    }
}

#alias end_remort_newbie {
    visit house;e;drop all.leather;drop all.hide;
    visit eremitage;dep all;with 1000000;u;rem all;put all chest;
    #var remort_equipped 0;
    recall;
    setguildpath;$basepath;prac_base_class;
    goguild;prac_$spec_class;
    #var skinningmaxed 0;
    recall;5nwn;
}

#alias handle_remort_level {
    #show <131>HANDLE REMORT LEVEL: %2;
    #nop send_to_comms_session <170>New level, $MSDP_CHARACTER_NAME - %2;
    #SWITCH {%2} {
        #CASE {2} { 
            initialize_remort;
            recall;
            #delay 3 {#show <131>Move to training tower;recall;2s3wn;buy purple;quaff purple;recall;4sd3s5enu;#action {The Entrance to the Halls of Evil} {#unaction {The Entrance to the Halls of Evil};prepare_towers_of_training;#delay 3 {,;.}}}
        };
        #CASE {5} { zone_end;recall;2s3wn;buy purple;quaff purple;recall;remort_quest_5 };
        #CASE {7} { 
            recall;2s3wn;buy purple;quaff purple;recall;
            #action {You enter a ring of mushrooms.} {#unaction {You enter a ring of mushrooms.};n;prepare_faerie_realm;cleartarget;,;.};
            #show <131>Move to Farie Realm;2s8w2sw2ses;enter mushroom
        };
        #CASE {8} { 
            #if {"$booster"=="Kaicha"} {};
            #if {"$booster"=="Liminal"} {
                #var {toggle[ranged]} {on};
                #action {You quaff a yellow potion of see invisible which dissolves.} {#unaction {You quaff a yellow potion of see invisible which dissolves.};recall;tell liminal hi;fol liminal;#delay 3 {gt liminal start}};
                #action {You feel very Zzzzzzzzz...} {gt liminal stop;wake;stand;recall;gt liminal start};
                recall;visit eremitage;u;get elven chest;get yellow chest;quaff yellow
            };
        };
        #CASE {10} { 
            #action {* * * You now have enough experience to gain %d levels! * * *} {#show <131>I can now gain %%1 levels;#if {%%1==5} {gt liminal stop;#delay 10 {goguild;prac armor;gain;gain;gain;gain;gain;spec $spec_class;wear elven;recall;#delay 3 {gt liminal start}}}} 
        };
        #CASE {15} { 
            #action {* * * You now have enough experience to gain %d levels! * * *} {#show <131>I can now gain %%1 levels;#if {%%1==4} {gt liminal stop;#delay 10 {goguild;gain;gain;gain;gain;recall;#delay 3 {gt liminal start}}}} 
        };
        #CASE {19} { 
            #action {* * * You now have enough experience to gain %d levels! * * *} {#show <131>I can now gain %%1 levels;#if {%%1==7} {gt liminal stop;#delay 10 {goguild;gain;gain;gain;gain;gain;gain}}} 
        };
        #CASE {24} { 
            #if {("$booster"=="Kaicha") && ($profession[refining][auto])} { 
                #var time_bloodline_handover $MSDP_UPTIME;
                #math temptime {$time_bloodline_handover - $time_bloodline_start};
                send_to_comms_session <170>Phase: $booster - $MSDP_CHARACTER_NAME (@return_min_sec{$temptime}) since start;
                #unvar temptime;
                visit house;dep all;#var booster {};with 150000;recall;#var tanningmaxed 0;5nwn 
            };
            #if {("$booster"=="Liminal") && ($profession[refining][auto]==0)} { 
                #var time_bloodline_handover $MSDP_UPTIME;
                #math temptime {$time_bloodline_handover - $time_bloodline_start};
                send_to_comms_session <170>Phase: $booster - $MSDP_CHARACTER_NAME (@return_min_sec{$temptime}) since start;
                #unvar temptime;
                visit house;dep all;with 150000;e;fol $MSDP_CHARACTER_NAME;fol $refresher;#var booster {};recall;#var tanningmaxed 0;5nwn 
            };
            #if {("$booster"=="") && ($profession[refining][auto])} { remorttan };
        };
        #CASE {29} { 
            #if {"$MSDP_CLASS"=="Ranger"} {recall;prac endurance;prac endurance;prac endurance;prac endurance;prac endurance};
            #if {$profession[refining][auto]} { remorttan };
        };
        #CASE {44} { 
            #if {"$MSDP_CLASS"=="Barbarian"} {prac endurance;prac endurance;prac endurance;prac endurance;prac endurance};
            #if {$profession[refining][auto]} { remorttan };
        };
        #CASE {50} { 
            #show <131>LEVEL 50 ACCOMPLISHED, NEXT STEP: end_remort_newbie;
            #var time_bloodline_50 $MSDP_UPTIME;
            #math temptime_50 {$time_bloodline_50 - $time_bloodline_start};
            #math temptime_tanning {$time_bloodline_50 - $time_bloodline_handover};
            send_to_comms_session <170>Phase: Lvl50 $MSDP_CHARACTER_NAME (@return_min_sec{$time_bloodline_50}) Tanning Phase:(@return_min_sec{$temptime_tanning}) since start;
            #unvar temptime_50;#unvar temptime_tanning;
        };
        #CASE {57} { 
            #if  {"$MSDP_CLASS"=="Barbarian"} { 
                #show <131>TIME TO PRAC TOUGHNESS;
                prac toughness;prac toughness;prac toughness;prac toughness;prac toughness
            };
            #show <131>TIME TO META STATS;
            recall;
            metastats
        };
        #CASE {60} { 
            #show <131>END OF JOURNEY, NEXT STEP: create bloodlines;
        };
        #DEFAULT {
            #show <131>NO LEVEL %2 ACTION DEFINED;
            #if { (20<$MSDP_LEVEL) && ($MSDP_LEVEL<51) && ($profession[refining][auto]) && ("$booster"=="")} {
                remorttan;
            }
        }
    };
}


#action {Ancestor %1 gains %2 hitpoints, %3 mana, and %4 stamina for gaining 50 levels.} {
    send_to_comms_session <170>Ancestor %1 gains: %2 hitpoints, %3 mana, and %4 stamina;
}

#action {* * * You now have enough experience to gain a level! * * *} {
    #if { (20<=$MSDP_LEVEL) && ($MSDP_LEVEL<51) && ($profession[refining][auto]) } {
        breakout;gogain
    }
}

#alias {metastats} {
    2s14w;enter tree;open door;w;
    #if { ($MSDP_BANK_GOLD<100000000) } {
        with all
    } {
        with 10000000
    };
    rem all;
    #10 buy str;#10 buy int;#10 buy wis;#10 buy dex;#5 buy maxcon;#15 buy con;
    dep all;
    16e2n;
    #ticker remind_to_relog {#show <131>NEED TO RELOG BEFORE STATS TAKE EFFEKT} 10;
    doMetaOrGain
}

#alias gogain {
    egre;home;
    #math nextlevel {$MSDP_LEVEL+1};
    #if { ($nextlevel==50) } { with 200000 } { with $goldlevel[$nextlevel] };
    egre;goguild;
    gain
}

#action {You now have %1 proficiency points available.} {#if ($tanningmaxed==0) {#show <131>We need to practice tanning;breakout;visit house;with 45000;egress;4nwnnn}}
#action {Wasat%1s Workshop} {#if ($tanningmaxed==0) {prac tanning};#if { ($skinningmaxed==0) && ($MSDP_LEVEL>49) } {prac skinning}}
#action {You practice tanning for a while...} {#if ($tanningmaxed==0) {prac tanning}}
#action {You practice skinning for a while...} {#if ($skinningmaxed==0) {prac skinning}}
#action {Wasat says, 'At rank 10, you know all that I can teach you in tanning.'} {#var tanningmaxed 1;remorttan}
#action {Wasat says, 'At rank 10, you know all that I can teach you in skinning.'} {#var skinningmaxed 1;#show <131>TIME TO MOVE TO GUMMTON - Next step: gogummton}
#action {Wasat says, 'You are too inexperienced!  You need 1000 experience to train tanning.'} {remorttan;#delay 30 {w;with 45000;recall;5nwn}}
#action {You do not have enough proficiency points left to learn what I can teach you.} {#if {$tanningmaxed==0} {remorttan};#if {$skinningmaxed==0} {#show <131>NOT ENOUGH PROFESSION POINTS, WE NEED TO REGRESS TANNING}}
#action {Wasat says, 'Come back later!  Hours are 1:00 - 24:00'} {#delay 10 l}

#alias remorttan {profon skinning;maton HouseOfAnasasi;#var profession[gather][selling] 0;visit house;dep all;e;get all.hide;tan hide}



#nop LEVEL 1-8
#alias remort_quest_1 {recall;3swn;buy south;sesd3s;quest;give south cleric}
#action {The cleric healer tells you, 'Great job!  While by no means exact, the maps will help you find your way more easily.'} {remort_quest_1a}
#alias remort_quest_1a {recall;2s4w12se;buy black;recall;10n7e2s2e;open flap;2n;quest;give black jorim;sac map}

#action {You are now on the quest: Wrong hole: Solo Target, Half dressed guy} {quest end;#delay 5 {quest}}
#alias remort_quest_5 {recall;3s2e2ne;quest}
#action {The big bartender tells you, 'My wife recently passed away along with my unborn child.' } {recall;2s4e7s2w;open door;2w;open door west;ws;recall;3s2e2ne}
#action {The big bartender tells you, 'Thank you so much take this for your troubles!'} {#delay 3 remort_quest_5a}

#alias remort_quest_5a {recall;2s6en;open gate n;n;open door w;w;quest;recall;2s4e7s2w;open door w;2wun;open door u;u;#delay 3 remort_quest_5b}
#alias remort_quest_5b {recall;2s6en;open gate n;n;open door w;w;;#delay 3 remort_quest_5c}
#alias remort_quest_5c {recall;2s6en;quest;open gate n;2n;open door n;ne;kill major}
#action {Old Major is dead!  R.I.P.} {w;open gate s;2s;open gate s;s} {1}
#action {The manor keeper tells you, 'In his death he will fertilize the ground. '} {
    #delay 3 {sac hat;#show <131>Proceed to Madam Zickva;#action {The Front Porch} {#unaction {The Front Porch};prepare_madam_zickva;#delay 3 {,;.}};recall;3s2e2nw}
}



#alias prac_base_class {
    recall;2s2e2s;
    #3 prac critical hits;
    #4 prac parry;
    #4 prac multi attack;
    #8 prac armor use;
    #9 prac edged weapons;
    #6 prac ranged weapons;
    #4 prac rescue;
    #3 prac lore;
    recall;
}

#alias prac_ranger {
    #3 prac camouflage;
    prac pass without;
    #4 prac ranged weapons;
    #10 prac dual wield;
    #2 prac critical hits;
    #5 prac bind wounds;
    #5 prac snapshot;
    #5 prac evade;
    #3 prac hawk eyes;
    #5 prac deepdraw;
    #2 prac scout;
    prac multi attack;
    #4 prac focus;
    #var focusStat dex;
    #var {toggle[focus]} {on};
    #var {toggle[camouflage]} {on};
    recall
}

#alias prac_barbarian {
    prac edged;
    #5 prac parry;
    #5 prac focus;
    prac scare;
    #5 prac warcry;
    #5 prac warpaint;
    prac multi attack;
    #10 prac two handed;
    #5 prac deathstroke;
    #var focusStat dex;
    #var {toggle[focus]} {on};
    #var {toggle[warcry]} {on};
    #var {toggle[warpaint]} {on};
    #var bot[damage][single] deathstroke;
    recall;
}

#alias {gogummton} {visit house;dep all;with 100000;egre;3s2es;book gumm}

#action {End of Frontier Pier at Gummton Outpost} {
    #if { ("$MSDP_ACCOUNT_NAME"=="Kruoill") && (("$MSDP_CLASS"=="Ranger") || ("$MSDP_CLASS"=="Barbarian")) && ($MSDP_LEVEL<60)} {
        initialize_wilderness
    };#elseif { ("$MSDP_ACCOUNT_NAME"=="Kruoill") && (("$MSDP_CLASS"=="Ranger") || ("$MSDP_CLASS"=="Barbarian")) && ($MSDP_LEVEL==60)} {
        end_wilderness;
    }
}

#alias initialize_wilderness {
    #if {$remort_equipped==0} { #nop First time we enter wilderness;
        #var time_bloodline_wilderness $MSDP_UPTIME;
        #math temptime_wilder {$time_bloodline_wilderness - $time_bloodline_start};
        send_to_comms_session <170>Phase: Arrived at Gummton. (@return_min_sec{$time_bloodline_wilderness}) since start;
        #unvar temptime_wilder;
        home;get all remort;wear all;get all case;wear all;put all case;get backpack case;
        #var remort_equipped 1;
        recall;
        #show <131>IN WILDERNESS AND EQUIPPED, NEXT STEP: join_gary_group;
        join_gary_group
    };
    profon skinning;
    #var autopickuphides 1;
    #var autotooth 0;
    #var toothcontainer backpack;
    bot_init;
    botrole damage;
    #ticker clearroom profroom 10;
}

#alias join_gary_group {
    #var regroup 1;
    #action {A glowing white portal appears.} {
        #if $regroup {
            #var regroup 0;
            enter portal;
            fol gary;
            #unaction {A glowing white portal appears.};
        };
        #action {You now follow Gary.} {
            #unaction {You now follow Gary.};
            #var time_bloodline_grouped $MSDP_UPTIME;
            #math temptime_group {$time_bloodline_grouped - $time_bloodline_start};
            send_to_comms_session <170>Phase: Grouped in Wilderness.  (@return_min_sec{$temptime_group}) since start;
            #unvar temptime_group;
        } {1}
    };
    tell gary portal me;
}

#alias end_wilderness {
    #var time_bloodline_theend $MSDP_UPTIME;
    #math time_bloodline_end {$time_bloodline_theend - $time_bloodline_start};
    send_to_comms_session <170>Phase: Arrived at Gummton. (@return_min_sec{$time_bloodline_end}) since start;
    #unvar time_bloodline_end;
    gt nohunt;recall;home;get all backpack;put all case;get all hole;put all case;rem all;put all case;rem all;put all case;
    #show <131>END OF JOURNEY, NEXT STEP: create bloodlines;
}



#alias prepare_towers_of_training {
    #action {A swordswoman is here, practicing with her blade.%1} {settarget 999 @mob_count{"%%1"} sword 1 1 1};
    #action {A foul-smelling goblin skulks about, avoiding your gaze.%1} {settarget 999 @mob_count{"%%1"} goblin 1 1 1};
    #action {A man dressed in robes of the blackest night tries to animate a corpse.%1} {settarget 999 @mob_count{"%%1"} necromancer 1 6 1};
    #action {A decrepit old hag cackles wickedly as she prepares something gruesome.%1} {settarget 999 @mob_count{"%%1"} hag 1 6 1};
    #action {A man stands back in the shadows, observing the others in silence.%1} {settarget 999 @mob_count{"%%1"} assa 1 6 1};
    #action {A princess from the drow underworld stares at you coldly.%1} {settarget 999 @mob_count{"%%1"} princess 1 6 1};
    #action {A young man aspiring to be an antipaladin is here, visiting the tower.%1} {settarget 999 @mob_count{"%%1"} antipaladin 1 6 2};
}

#alias prepare_madam_zickva {
    #action {A female guest is here half passed out in her chair.%1} {settarget 999 @mob_count{"%%1"} guest 5 9 1};
    #action {A male guest is here half passed out in his chair.%1} {settarget 999 @mob_count{"%%1"} guest 5 9 1};
    #action {A lady is here flirting with the guys.%1} {settarget 999 @mob_count{"%%1"} flirt 6 7 2};
    #action {A guy is here flirting with the gals.%1} {settarget 999 @mob_count{"%%1"} flirt 6 7 2};
}

#alias prepare_faerie_realm {
    #action {An ochre sprite flies around in circles.%1} {settarget 999 @mob_count{"%%1"} sprite 1 10 1};
    #action {A green sprite stands here, waving his half-foot sword.%1} {settarget 999 @mob_count{"%%1"} sprite 1 10 1};
    #action {A grey sprite looks at you menacingly, its sickly wings keeping it aloft.%1} {settarget 999 @mob_count{"%%1"} sprite 1 10 1};
    #action {A white sprite is flitting about.%1} {settarget 999 @mob_count{"%%1"} sprite 1 10 1};
    #action {An umber sprite leans against a tree.%1} {settarget 999 @mob_count{"%%1"} sprite 1 10 1};
    #action {An ashen sprite flits about.%1} {settarget 999 @mob_count{"%%1"} sprite 1 10 1};
    #action {A forest sprite stands, laughing at you.%1} {settarget 999 @mob_count{"%%1"} sprite 1 10 1};
    #action {A small green leprechaun watches over his pot of gold.%1} {settarget 999 @mob_count{"%%1"} leprechaun 1 10 1};
    #action {A dark tree stands, roots out of the ground.%1} {settarget 999 @mob_count{"%%1"} tree 1 10 1};
    #action {A satyr stomps its hoofs and looks ready to tussle.%1} {settarget 999 @mob_count{"%%1"} satyr 1 10 1};
}

#nop -- vim: syntax=tt
