#nop Walker debug


#var hub 34595;
#var autowalk 1;
#var autowalk_delay 1;


#event {MAP ENTER ROOM} {
    room_clear;
    #delay $autowalk_delay {
        #if {"$MSDP_ROOM_VNUM"=="?"} {
            #map get roomvnum cur_room_vnum;
            #nop show <143>MAP ENTER EVENT: GOT FROM MAP $cur_room_vnum
        }; #else {
            #var cur_room_vnum $MSDP_ROOM_VNUM;
            #nop show <143>MAP ENTER EVENT: GET FROM MSDP $cur_room_vnum;
        };
        #show <174>DELAYED MAP ENTER ROOM: $cur_room_vnum  Pos:$botPositive   Neg:$botNegative  Mobs: $tellu_mobs_in_room;
        evaluate_room
    }
}


#alias mob_dead {
    #if {"$char_entangled"!=""} {
        #format char_entangled %l $char_entangled;
        gt all chop $char_entangled
    };
    #var char_entangled {};
    #show <131>MOB DEAD - FIND NEXT MOVE;
}

#alias evaluate_room {
    #nop show <131>EVALUATE ROOM CALLED;
    #if {$cur_room_vnum==$hub} {
        #nop show <131>WE ARE AT HUB;
        load_next_zone;
    };
    #else {
        #if {$botPositive!=1} {
            #nop show <131>NO POSITIVE TARGETS;
            find_next_move;
        };
        #else {
            #if { ($botNegative>0) || ($tellu_mobs_in_room>1) } {
                #nop show <131>BAD GUYS OR TOO MANY MOBS;
                find_next_move;
            };
            #else {
                #show <131>EVALUATE ROOM - ONLY ONE GOOD TARGET;
                #if {@verifyTelluZone{}} {
                    #show <131>ROOM VERIFIED - KILL TARGET;
                    #delay 5 {#if {$cur_opponents==0} {#SHOW <131>COMBAT DIDNT START - MOVE ON;find_next_move}}
                };#else {
                    #nop show <131>ROOM NOT VERIFIED - DONT KILL TARGET;
                }
            }
        };
    }
}    


#alias find_next_move {
    #nop show <147>FIND NEXT MOVE CALLED;
    #if {$cur_room_vnum==$destination} {
        #nop show <147>At destination: REMOVE CURRENT DESTINATION - LOAD NEXT DESTINATION;
        #list waypoints[$tellu_zone] delete 1;
        load_route;
    }; #elseif {$destination>0} {
        #nop show <147>FIND PATH TOWARDS MOVE: $destination;
        load_route;
    }; #else {
        #nop show <147>No destination;
        #nop
    };
    #nop show <147>MOVE;
}



#alias load_next_zone {
    #nop show <172>LOAD NEXT ZONE CALLED;
    #math tellu[zindex] {$tellu[zindex]+1};
    #if {$tellu[zindex] <= &tellu[zone][]} {
    };#else {
        #var tellu[zindex] 1;
    };
    #var tellu_zone $tellu[zone][$tellu[zindex]];
    #nop show <172>NEXT TELLU ZONE: [$tellu[zindex]:&tellu[zone][]] $tellu_zone;
    #var waypoints {};
    #var waypoints[north] {34502,34501,34508,34532,34503,34552,34558,34533,34504,34529,34595,34595};
    #var waypoints[west] {34542,34549,34550,34573,34547,34521,34523,34571,34544,34568,34595,34595};
    #var waypoints[east] {34567,34560,34559,34512,34562,34538,34536,34514,34565,34517,34595,34595};
    #list waypoints[$tellu_zone] explode {,};
    load_route;
}


#alias load_route {
    #nop show <132>LOAD ROUTE CALLED;
	#list waypoints[$tellu_zone] get 1 destination;
    #nop show <132>We are in $tellu_zone  Current: $cur_room_vnum  Destination: $destination;
    #map find $destination;
    #if {$autowalk} {
        smart_move;
    }
}






#var botPostitive 0;
#var botNegative 0;
#var tellu_mobs_in_room 0;

#var autowalkspeed 0.5;
#var waypointdelay 0.5;
#var zonedelay 5;
#var tellu_kill_delay 4;
#var tellu_allowed_downtime 60;
#var autowalk 1;

#var tell_start_kill 1; #nop Will initiate kill, ask for rescue
#var tell_start_kill_delay 0.1; #nop time we wait before initiating combat

#var tellu_stats[average_kill_time] 0;
#var tellu_stats[revive] 0;
#var tellu_stats[fury] 0;
#var tellu_stats[prudent] 0;
#var tellu_stats[scarab-kin] 0;
#var tellu_stats[topiary] 0;
#var tellu_stats[tundra-snap] 0;


#var tellu_mode normal;

#var tellu[zindex] 0;
#var tellu[zone] {west,north,east};
#list tellu[zone] explode {,};

#var tellu_rooms[north] {34509,34531,34556,34529,34502,34527,34551,34526,34501,34528,34553,34530,34508,34532,34506,34554,34503,34552,34505,34557,34558,34533,34507,34555,34504,34529,34556,34531,34509};
#var tellu_rooms[west] {34543,34569,34518,34568,34542,34570,34520,34572,34549,34574,34525,34575,34550,34573,34548,34524,34547,34521,34546,34522,34523,34571,34545,34519,34544,34568,34518,34569,34543};
#var tellu_rooms[east] {34566,34516,34541,34517,34567,34515,34539,34513,34560,34511,34534,34510,34559,34512,34561,34535,34562,34538,34563,34537,34536,34514,34564,34540,34565,34517,34541,34516,34566};
#list tellu_rooms[north] explode {,};
#list tellu_rooms[west] explode {,};
#list tellu_rooms[east] explode {,};

#var tell_mobs[east][normal][Forest] {"snap", "scarab", "wolf"};
#var tell_mobs[east][normal][Desert] {"topiary", "snap", "wolf"};
#var tell_mobs[east][normal][Tundra] {"scarab", "topiary"};
#var tell_mobs[east][clear][Forest] {"tree", "topiary"};
#var tell_mobs[east][clear][Desert] {"scarab"};
#var tell_mobs[east][clear][Tundra] {"wolf", "snap"};

#var tell_mobs[west][normal][Forest] {"snap", "topiary", "wolf"};
#var tell_mobs[west][normal][Desert] {"scarab", "topiary"};
#var tell_mobs[west][normal][Tundra] {"scarab", "snap", "wolf"};
#var tell_mobs[west][clear][Forest] {"scarab"};
#var tell_mobs[west][clear][Desert] {"wolf", "snap"};
#var tell_mobs[west][clear][Tundra] {"tree", "topiary"};

#var tell_mobs[north][normal][Forest] {"scarab", "topiary"};
#var tell_mobs[north][normal][Desert] {"snap", "scarab", "wolf"};
#var tell_mobs[north][normal][Tundra] {"topiary", "snap", "wolf"};
#var tell_mobs[north][clear][Forest] {"wolf", "snap"};
#var tell_mobs[north][clear][Desert] {"tree", "topiary"};
#var tell_mobs[north][clear][Tundra] {"scarab"};


#alias eval_mobs_in_room {
    #var tellu_mob_in_room %1;
    #math tellu_mobs_in_room {$tellu_mobs_in_room + $tellu_mob_in_room};
    #regexp {$tell_mobs[$tellu_zone][$tellu_mode][$MSDP_ROOM_TERRAIN]} {$tellu_target} 
        {#show <172> [$tellu_mob_in_room] TRUE - $tellu_mode $cur_room_vnum;#var botPositive $tellu_mob_in_room}
        {#show <131> [$tellu_mob_in_room] FALSE - $tellu_mode $cur_room_vnum;#var botNegative $tellu_mob_in_room} 
}

#function verifyTelluZone {
    #var tellu_zone_correct 0;
    #regexp {$tellu_rooms[$tellu_zone]} {$cur_room_vnum} {#var tellu_zone_correct 1};
    #return $tellu_zone_correct
}

#alias room_clear {
    #var tellu_mobs_in_room 0;
    #var botPositive 0;
    #var botNegative 0;
    #var tellu_target {};
}


#action {A large, glowing scarab stands here in a defensive posture.%1} {#var tellu_target scarab;eval_mobs_in_room @mob_count{"%1"}}
#action {A large wolf, with icy-blue fur stands here observing.%1} {#var tellu_target wolf;eval_mobs_in_room @mob_count{"%1"}}
#action {An extremely stout tree stands here, wavering slowly.%1} {#var tellu_target topiary;eval_mobs_in_room @mob_count{"%1"}}
#action {A Tellurian scarab-kin is here looking alert.%1} {#var tellu_target scarab;eval_mobs_in_room @mob_count{"%1"}}
#action {A Tellurian tundra-snap is here looking alert.%1} {#var tellu_target wolf;eval_mobs_in_room @mob_count{"%1"}}
#action {A Tellurian topiary is here looking alert.%1} {#var tellu_target topiary;eval_mobs_in_room @mob_count{"%1"}}

#var tell_mobs[east][normal][Forest] {"snap", "scarab", "wolf"};
#var tell_mobs[east][normal][Desert] {"topiary", "snap", "wolf"};
#var tell_mobs[east][normal][Tundra] {"scarab", "topiary"};
#var tell_mobs[east][clear][Forest] {"tree", "topiary"};
#var tell_mobs[east][clear][Desert] {"scarab"};
#var tell_mobs[east][clear][Tundra] {"wolf", "snap"};

#var tell_mobs[west][normal][Forest] {"snap", "topiary", "wolf"};
#var tell_mobs[west][normal][Desert] {"scarab", "topiary"};
#var tell_mobs[west][normal][Tundra] {"scarab", "snap", "wolf"};
#var tell_mobs[west][clear][Forest] {"scarab"};
#var tell_mobs[west][clear][Desert] {"wolf", "snap"};
#var tell_mobs[west][clear][Tundra] {"tree", "topiary"};

#var tell_mobs[north][normal][Forest] {"scarab", "topiary"};
#var tell_mobs[north][normal][Desert] {"snap", "scarab", "wolf"};
#var tell_mobs[north][normal][Tundra] {"topiary", "snap", "wolf"};
#var tell_mobs[north][clear][Forest] {"wolf", "snap"};
#var tell_mobs[north][clear][Desert] {"tree", "topiary"};
#var tell_mobs[north][clear][Tundra] {"scarab"};





#action {A Tellurian blood-mage is hovering here.} {kill mage;#var tellu_target mage;: marks $tellu_target for death.}
#action {A terrifying scream pierces the ears as GRODAN emerges from his slumber!} {#show UPPER FLOOR OPEN}

#action {A Tellurian fury is hovering here.} {#var tellu_target fury;#if { ("$MSDP_CLASS"=="Wizard") || ("$MSDP_CLASS"=="Prophet")} {#var bot[damage][single]dmagic}}
#action {A Tellurian fury steps out of the shadows.} {#show <131>FURY JOINED THE FIGHT!!!}

#action {A Tellurian elder is hovering here.} {#var tellu_target elder;: marks $tellu_target for death.}

#action {Grodan is here looking alert.} {#var tellu_target grodan;#if { ("$MSDP_CLASS"=="Wizard") || ("$MSDP_CLASS"=="Prophet")} {#var bot[damage][single] dmagic}}

#action {Grodan gleams as the remaining elders infuse him with their life essence.} {#show <131>STILL ELDERS ALIVE, MIRACLE!!!}

#action {Grodan glimmers as the remaining visages infuse him with their life essence.} {#show <131>VISAGES SPAWNED, MIRACLE!!!}
#action {A visage of Grodan is hovering here.} {#var tellu_target grodan;: marks $tellu_target for death.}
#highlight {All of Telluria shakes and rumbles as the ground shifts.} {reverse yellow}

#action {A Tellurian %1 says, 'Ahh, yes.%2The %3 shall revive my body!'} {
    #delay $tellu_kill_delay {actionCleared};
    #var tellu_stats[%1] @increment{$tellu_stats[%1]};
    #var tellu_stats[revive] @increment{$tellu_stats[revive]};
    #var lastkilltime $MSDP_UPTIME;
    #math tellu_kill_timer { $MSDP_UPTIME - $tellu_kill_timer };
    #show \n<172>GOOD KILL on [%1] in [%3]!!    zone:[$tellu_zone]  KillTime: $tellu_kill_timer(s);
    #nop addAverageKillTime $tellu_kill_timer
};
#action {A Tellurian %1 says, 'Ahh, yes.%2The %3 shall revive my body, and fuel my FURY!'} {
    #delay $tellu_kill_delay {actionCleared};
    #var tellu_stats[%1] @increment{$tellu_stats[%1]};
    #var tellu_stats[fury] @increment{$tellu_stats[fury]};
    #var lastkilltime $MSDP_UPTIME;
    #math tellu_kill_timer { $MSDP_UPTIME - $tellu_kill_timer };
    #show \n<Bfca><F000>FURY KILL on [%1] in [%3]!!    zone:[$tellu_zone]  KillTime: $tellu_kill_timer(s);
    #nop addAverageKillTime $tellu_kill_timer
};
#action {A Tellurian %1 says, 'Ahh, yes.%2The %3 shall revive my body, and my FURY!'} {
    #delay $tellu_kill_delay {actionCleared};
    #var tellu_stats[%1] @increment{$tellu_stats[%1]};
    #var tellu_stats[fury] @increment{$tellu_stats[fury]};
    #var lastkilltime $MSDP_UPTIME;
    #math tellu_kill_timer { $MSDP_UPTIME - $tellu_kill_timer };
    #show \n<Bfca><F000>FURY KILL on [%1] in [%3]!!    zone:[$tellu_zone]  KillTime: $tellu_kill_timer(s);
    #nop addAverageKillTime $tellu_kill_timer
};
#action {A Tellurian %1 says, 'Ahh, yes.%2The %3, a prudent resting place.'} {
    #delay $tellu_kill_delay {actionCleared};
    #var tellu_stats[%1] @increment{$tellu_stats[%1]};
    #var tellu_stats[prudent] @increment{$tellu_stats[prudent]};
    #var lastkilltime $MSDP_UPTIME;
    #math tellu_kill_timer { $MSDP_UPTIME - $tellu_kill_timer };
    #show \n131>VERY BAD KILL on [%1] in [%3]!!    zone:[$tellu_zone]  KillTime: $tellu_kill_timer(s);
    gt BAD KILL;
    #nop addAverageKillTime $tellu_kill_timer
};

#action {A Tellurian %1 comes to a Tellurian %2's aid.} {safetokill {%1} {%2} {} {JOINED}}

#action {A Tellurian %1 arrives from the %2.} {safetokill {%1} {} {%2} {ARRIVES}}

#action {A Tellurian %1 utters the words 'unsohaiorz'} {#nop show <131>%1 ENDS COMBAT}
#action {A Tellurian %1 steps out of the shadows.} {#nop show <131>%1 JOINER!!!!}

#alias safetokill {
    #nop %1 joiner, %2 aided, %3 direction, %4 action ;
    #show \n<132>>%1< >%2< >%3< >%4<;
    #switch {"%1"} {
        #case {"tundra-snap"} {#var tellu_joiner wolf};
        #case {"topiary"} {#var tellu_joiner topiary};
        #case {"scarab-kin"} {#var tellu_joiner scarab};
    };
    #regexp {$tell_mobs[$tellu_zone][$tellu_mode][$MSDP_ROOM_TERRAIN]} {$tellu_joiner} 
    {
        #nop MOB in safe room;
        #if {"%4"=="JOINED"} {
            #if {"@group_leader{}"=="$MSDP_CHARACTER_NAME"} {gt $tellu_joiner JOINED %2, SAFE kill} {#show <132>$tellu_joiner JOINED %2, SAFE kill};
        };#else {
            #show <172>$tellu_joiner ARRIVED from %3, SAFE KILL - [$tellu_zone]  [$tellu_mode]  [$MSDP_ROOM_TERRAIN]
        }
    }
    {
        #nop MOB in BAD room;
        #if {"%4"=="JOINED"} {
            #if {"@group_leader{}"=="$MSDP_CHARACTER_NAME"} {gt $tellu_joiner JOINED %2, BAD kill} {#show <132>$tellu_joiner JOINED %2, BAD kill};
        };#else {
            #show <131>$tellu_joiner ARRIVED from %3, BAD KILL - [$tellu_zone]  [$tellu_mode]  [$MSDP_ROOM_TERRAIN]
        }
    };
    #var tellu_joiner {};
}



#function visualizeOdometer {
    #var odobar {};
    #local _temp_odo_bar $_meter[KILLS];
    #list _temp_odo_bar sort;
    #list _temp_odo_bar reverse;
    #local _temp_odo_bar_max $_temp_odo_bar[0];

    #loop 1 &_meter[KILLS][] id {
        #math _odo_bar_current { @pct{$_meter[KILLS][$id];$_temp_odo_bar_max} };
        #format _odo_bar_current {%d} {$_odo_bar_current};
        #if {$MSDP_WORLD_TIME != $id} {
            #cat odobar {@colorPctBlock{$_odo_bar_current}}
        };#else {
            #cat odobar {<bffffff><f000000>â– }
        }
    };
    #cat odobar {<B538d87>};
    #return $odobar
}



#alias tellu_bot_info {
	#if $autowalk {
        #math timesincelastkill { $MSDP_UPTIME - $lastkilltime };
		#format tellu_bot_info[1] {<B538d87><Fffffff>Zone:<Fd5d8d6>%-5s <Fffffff>Autokill:<Fd5d8d6>%s (%ss) <Fffffff>Autowalk:<Fd5d8d6>%s} {$tellu_mode} {$tell_start_kill} {$tell_start_kill_delay} {$autowalk};
        #format tellu_bot_info[2] {<B538d87><Fffffff>Walk:<Fd5d8d6>%-3s <Fffffff>WayP:<Fd5d8d6>%s <Fffffff>Delays Kill:<Fd5d8d6>%s <Fffffff>Zone:<Fd5d8d6> %s } {$autowalkspeed} {$waypointdelay} {$tellu_kill_delay} {$zonedelay};
        #format tellu_bot_info[3] {<B538d87><Fffffff>LastKill:<Fd5d8d6>%+3s(s) <Fffffff>Allowed:<Fd5d8d6>%s(s)} {@tellu_lastkilltime{}} {$tellu_allowed_downtime};
        #format tellu_bot_info[4] {<B538d87><Fffffff>Prudent:<Fd5d8d6> %-1s <Fffffff>Revive:<Fd5d8d6>%s <Fffffff>Fury:<Fd5d8d6>%s} {$tellu_stats[prudent]} {$tellu_stats[revive]} {$tellu_stats[fury]};
        #format tellu_bot_info[5] {<B538d87><Fffffff>Odometer: %s} {@visualizeOdometer{}};
    };
	#else {
		#var tellu_bot_info {Tellu Bot: offline};
	};
    #if {@is_alias{display_right_tiles}} {display_right_tiles};
}











