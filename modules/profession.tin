#nop --
#nop Class Definitions v0.01
#nop --

#var profession-description {Utilities for botting}
#var profession-help {
Commands:\n
\tprofon <profession> / profoff      profreset \n
\ttanon / tanoff  \n
\tAlias: skinlist  \n
\n
}

#nop --
#nop Modloader Stuff
#nop --

#alias profession-register {
	#if {@isloaded{modloader}} {

		register_module profession
	} {
		#class profession kill
	}
}


#NOP #######################################################
#NOP Profession unique variables
#NOP #######################################################

#nop Gathering an be: skinning, butcher, extract
#var profession[gather][type] skinning
#var profession[gather][auto] 1
#var profession[gather][all] 0
#var profession[gather][success] 0
#var profession[gather][total] 0
#var profession[gather][receiver] {} ;#nop Empty will donate to own house, or else to <value> house

#alias profon 	{
	#var profession[gather][auto] 1;
	generate_profession_info;
	#show Will start gathering: %1;
	#SWITCH {"%1"} {
		#CASE {"skinning"} {#var profession[gather][type] %1;#var profession[gather][all] 0};
		#CASE {"butcher"} {#var profession[gather][type] %1;#var profession[gather][all] 0};
		#CASE {"extract"} {#var profession[gather][type] %1;#var profession[gather][all] 0};
		#CASE {"all"} {#var profession[gather][all] 1};
		#DEFAULT {#show Not a supported profession}
	}
}
#alias profoff 	{#var profession[gather][auto] 0;generate_profession_info;#show Will no longer gather anything;#var profession[gather][all] 0}
#alias {profreset} {#var profession[gather][success] 0;#var profession[gather][total] 0;generate_profession_info;}


#nop Refining can be: tan, cook, carve
#var profession[refining][type] tanning
#var profession[refining][auto] 0
#var profession[refining][reset] 1
#var profession[refining][start] 0;
#var profession[refining][startgold] 0;
#var profession[refining][startexp] 0;

#var profession[refresher] Krell
#var hidespickedup 0;



#NOP #######################################################
#NOP Profession panel
#NOP #######################################################
event_register {variable profession[gather][total]} {right} {generate_profession_info};
#alias generate_profession_info {
	#if $profession[gather][auto] {
		#if {"$profession[gather][receiver]" != ""} {
			#var profession_gather_info {$profession[gather][type] ($profession[gather][success]/$profession[gather][total]) @pct{$profession[gather][success];$profession[gather][total]} %  DonateTo: $profession[gather][receiver]};
		};
		#else {
			#var profession_gather_info {$profession[gather][type] ($profession[gather][success]/$profession[gather][total]) @pct{$profession[gather][success];$profession[gather][total]} %  DonateTo: Self};
		}
	};
	#else {
		#var profession_gather_info {Gathering: Offline};	
	};
	#if $profession[refining][auto] {
		#var profession_gather_info {$profession_gather_info \n$profession[refining][type]:$profession[refining][auto]}
	};#else  {
		#var profession_gather_info {$profession_gather_info \nRefining: Offline}
	};
    #if {@is_alias{display_right_tiles}} {display_right_tiles};
}

#if {&{right_tiles}} {
	set_right_tile 11 <B5f819d> profession_gather_info 2 tile;
	generate_profession_info;
}

#alias {do_gather} {
	#SWITCH {"$profession[gather][type]"} {
		#CASE {"skinning"} {do_skin %1};
		#CASE {"butcher"} {do_butcher %1};
		#CASE {"extract"} {do_extract %1};
		#DEFAULT {#nop}
	}; 
}




#NOP #######################################################
#NOP Professions - Skinning;
#NOP #######################################################

#NOP Entered new zone, update corpses to be skinned

#alias {do_skin} {
	#if $profession[gather][auto] {
		#if $profession[gather][all] {
			queueAdd any {skin corpse};
		};#else {
			#show <121><<< Skin? %0 >>>;
			#if { &zone[skinlist]["%0"]>0 } {
				queueAdd any {skin corpse};
			}
		}
	}
}


#ACTION {You skin some %1 hide from} {
	#show Skin type:[<131>%1];
	#math profession[gather][total] {$profession[gather][total]+1};
	#switch {"%1"} {
		#case {"junk"} {sac hide};
		#case {"rough"} {sac hide};
		#case {"average"} {donate hide home};
		#default {
			#if { "$profession[gather][receiver]"=="" } {
				donate hide home;
			};
			#else {
				give hide $profession[gather][receiver];
			};
			#math profession[gather][success] {$profession[gather][success]+1}
		}
	};
	generate_profession_info;
}



#NOP #######################################################
#NOP Professions - Chirurgy;
#NOP #######################################################

#NOP Entered new zone, update corpses to be extracted
#var zone[extractlist] {{"A kakapo"}{} {"A puffin"}{} {"A giant petrel"}{} {"A common murre"}{} {"A skua"}{} {"A northern gannet"}{} {"A sooty tern"}{} {"A weka"}{} {"A takahe"}{} {"A pelegornis"}{} {"A royal albatross"}{} {"A spectacled cormorant"}{} {"A giant moa"}};

#alias {do_extract} {
	#if $profession[gather][auto] {
		#if $profession[gather][all] {
			queueAdd priority {extract corpse};
		};#else {
			#show <121><<< Extract? %0 >>>;
			#if { &zone[extractlist]["%0"]>0 } {
				queueAdd priority {extract corpse};
			}
		}
	}
}


#ACTION {You extract some %1 bone from} {
	#show Bone type:[<131>%1];
	#math profession[gather][total] {$profession[gather][total]+1};
	#switch {"%1"} {
		#case {"junk"} {sac bone};
		#case {"rough"} {sac bone};
		#case {"average"} {donate bone home};
		#default {
			#if { "$profession[gather][receiver]"=="" } {
				donate bone home;
			};
			#else {
				give bone $profession[gather][receiver];
			};
			#math profession[gather][success] {$profession[gather][success]+1}
		}
	};
	generate_profession_info;
}



#NOP #######################################################
#NOP Auto Profession 
#NOP #######################################################
#alias maton {
	#var profession[refining][auto] 1;
	generate_profession_info;
	#var profession[refining][start] $MSDP_UPTIME;
	#var profession[refining][startgold] $MSDP_BANK_GOLD;
	#var profession[refining][startexp] $MSDP_EXPERIENCE;
	#var profession[refining][sold] 0;
	#show Will auto process materials from %1;
	#var profession[mat_location] %1;
	#SWITCH {"$profession[gather][type]"} {
		#CASE {"tanning"} {
			#var profession[str_mat] {hide};
			#var profession[str_pickup] {get all.hide};
			#var profession[str_action] {tan hide};
			#var profession[str_sack] {sac hide};
			#var profession[refine_location] {visit houseofanasasi;e};
		};
		#CASE {"butcher"} {
			#var profession[str_mat] {meat};
			#var profession[str_pickup] {get all.meat};
			#var profession[str_action] {cook meat};	
			#var profession[str_sack] {sac meat};
			#var profession[refine_location] {visit houseofanasasi;e};
		};
		#CASE {"extract"} {
			#var profession[str_mat] {bone};
			#var profession[str_pickup] {get all.bone};
			#var profession[str_action] {carve bone};	
			#var profession[str_sack] {sac bone};
			#var profession[refine_location] {3s2w;open gate;2s;open door;s2u4es};
		};
		#DEFAULT {#nop}
	}; 

}

#alias matoff {
	#math profession[refining][earnedgold] {$MSDP_BANK_GOLD - $profession[refining][startgold]};
	#math profession[refining][earnedexp] {$MSDP_EXPERIENCE - $profession[refining][startexp]};
	#math profession[refining][averagesell] {$profession[refining][earnedexp] / $profession[refining][sold]};
	#math profession[refining][runtime] {$MSDP_UPTIME - $profession[refining][start]};
	#var profession[refining][auto] 0;
	generate_profession_info;
	#show Will no longer process $profession[str_mat];
	gt <<< Processing session done: $profession[gather][type] >>>;
	gt Time: [@return_min_sec{$profession[refining][runtime]}]  Processed: [$profession[refining][sold]] Average: [$profession[refining][averagesell]]  Gold: [@pretty_number{$profession[refining][earnedgold]}]  Exp: [@pretty_number{$profession[refining][earnedexp]}];
}

#action {You {tan|carve} some %1 {hide|bone} from %2 into some %3 {leather|bone} from %4.} {
	#if {$profession[refining][auto]} {
		#show Material Quality:[<131>%3];
		#switch {"%3"} {
			#case {"flawless"} {
				#if {"$profession[gather][type]"=="extract"} {donate $profession[str_mat] home;};
				$profession[str_action]
			};
			#case {"divine"} {
				#if {"$profession[gather][type]"=="extract"} {donate $profession[str_mat] home;};
				$profession[str_action]
			};
			#default {
				#if {"$profession[gather][type]"=="extract"} {donate $profession[str_mat] home;};
				$profession[str_action]
			}
		}
	}
}

#action {You fail to improve it.} {#if $profession[refining][auto] $profession[str_action]}
#action {Oops, you cracked it a little.} {#if $profession[refining][auto] $profession[str_action]}
#action {You are exhausted!} {#if $profession[refining][auto] {#delay waitforrefresh {$profession[str_action]} {3}}}
#action {The some %0 bone from %1 isn't high enough quality to carve.} {#if $profession[refining][auto] {$profession[str_sack];$profession[str_action]}}
#action {You can't carve that.} {donate bone home;$profession[str_action]}

#action {You must also specify what you want to {tan|carve}.} {
	#var total_mats 0;
	recall;
	#if { ($profession[refining][reset]) && ($profession[refining][auto]) } {
		#if {"$profession[gather][type]"=="extract"} {
			#var profession[refining][reset] 0;
			pickup_mats;
		};
		#if {"$profession[gather][type]"=="tanning"} {
			#var profession[refining][reset] 0;
			2s3es;
			sell leather;
		};
	};
	#else {
		#show no more $profession[str_mat] left;
		matoff;
	}
}


#alias pickup_mats {
	#if { $profession[refining][auto] } {
		visit $profession[mat_location];
		e;
		$profession[str_pickup];
		countmats;
		recall;
		$profession[refine_location];
		$profession[str_action];
	}
}


#action {Your personal donation fairy whisks away the bone to your manor.} {#variable profession[refining][sold] @increment{$profession[refining][sold]}}

#action {Some {hide|bone} from %2 lies here. (%3)} {#math total_mats {$total_mats+%3}}
#action {You pick up %1 {hides|bones}.} {#var profession[refining][reset] 1;#var total_mats_pickedup %1;#math total_mats_remaining {$total_mats-%1}}
#alias countmats {#delay removetotal {gt $profession[refining][sold] sold, $total_mats_remaining remaining} {5}}


#NOP #######################################################
#NOP Vendor actions 
#NOP #######################################################
#action {The tanner tells you, 'It appears that %0 was previously donated and I don't buy donated items!'} {sac leather;sell leather}
#action {The tanner tells you, 'I'll give you %1 coins for that.'} {#variable profession[refining][sold] @increment{$profession[refining][sold]};sell leather}
#action {The tanner tells you, 'Please empty a leather backpack first.'} {visit houseofanasasi;dep all;pickup_mats}
#action {The tanner tells you, 'Are you sure you have that?'} {visit houseofanasasi;dep all;pickup_mats}
